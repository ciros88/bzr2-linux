# Maintainer: Ciro Scognamiglio <ciro.scognamiglio88 (at) gmail.com>
# Contributor: Ciro Scognamiglio <ciro.scognamiglio88 (at) gmail.com>

pkgname='bzr-player'
_pkgname='BZR Player'
pkgver='2.0.69'
pkgrel='1'
pkgdesc='Audio player supporting a wide types of multi-platform exotic file formats'
arch=('i686' 'x86_64')
url="http://bzrplayer.blazer.nu"
license=('GPL3')
depends=('wine' 'winetricks' 'hicolor-icon-theme')
makedepends=('gendesk' 'libarchive')
optdepends=("xorg-xrdb") #TODO
options=(!strip)
zip="BZR-Player-$pkgver.zip"
installer="bzr2_installer.sh"
mimes="x-bzr-player.xml"
source=("$zip::http://bzrplayer.blazer.nu/getFile.php?id=${pkgver}"
  "https://raw.githubusercontent.com/ciros88/bzr2-linux-installer/master/$installer"
  "https://raw.githubusercontent.com/ciros88/bzr2-linux-installer/master/$mimes")
noextract=($zip)
sha256sums=('26eb069230a0214f1d48bd4a7dace5be7ff5ce93fa3bcdf6f61261929a04e595'
  'SKIP'
  'SKIP')

# https://wiki.archlinux.org/title/Creating_packages
# https://wiki.archlinux.org/title/PKGBUILD
# https://wiki.archlinux.org/title/Wine_package_guidelines
# https://wiki.archlinux.org/title/desktop_entries#gendesk

prepare() {
  mkdir -p "${pkgname}-bin"
  bsdtar -xf "$zip" -C "${pkgname}-bin"

  mime_types_supported=()
  mapfile -t mime_types_supported < <(sed -n "\|mime_types_supported=(| , \|)|{p; \|)|q}" "$installer" |
    sed -e 's:mime_types_supported=(::g' -e 's:)::g' -e 's: :\n:g' | sed '/^[[:space:]]*$/d')

  for mime_type in "${mime_types_supported[@]}"; do
    desktop_entry_mime_types="$desktop_entry_mime_types$mime_type;"
  done

  desktop_entry_mime_types="${desktop_entry_mime_types%?}"

  gendesk -n -f --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name="$_pkgname" \
    --genericname='Audio player' \
    --exec="" \
    --icon="$pkgname" \
    --categories='AudioVideo;Audio;Player;Music' \
    --mimetype="$desktop_entry_mime_types"
}

package() {
  install -dm755 "$pkgdir/usr/share"
  cp -a "${pkgname}-bin" "$pkgdir/usr/share/$pkgname"
  install -Dm644 "$mimes" "$pkgdir/usr/share/mime/packages/$mimes"
  install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  for size in 16 22 24 32 48 64 128 256 512; do
    install -Dm644 "$pkgdir/usr/share/$pkgname/resources/icon.png" \
      "$pkgdir/usr/share/icons/hicolor/$size/apps/$pkgname.png"
  done
}
